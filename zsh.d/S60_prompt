#!/bin/zsh

autoload -Uz vcs_info

zstyle ':vcs_info:*' stagedstr '%F{28}●'
zstyle ':vcs_info:*' unstagedstr '%F{11}●'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{11}%r'
zstyle ':vcs_info:*' enable git svn

function count_todos {
  if [ -e '.project_todos.sh' ]; then
    echo "TODOS: $(./.project_todos.sh)"
  fi
}

function battery_charge {
  echo $(battery.py) 2> /dev/null
}

function prompt_char {
    git branch >/dev/null 2>/dev/null && echo '±' && return
    hg root >/dev/null 2>/dev/null && echo '☿' && return
    echo '○'
}

precmd () {
    if [[ -z $(git ls-files --other --exclude-standard 2> /dev/null) ]] {
        zstyle ':vcs_info:*' formats '%F{green}%b%u'
    } else {
        zstyle ':vcs_info:*' formats '%F{green}%b%u%F{red}●'
    }

    vcs_info
}

function setprompt {
  ###
  # Need this so the prompt will work.
  setopt prompt_subst

  ###
  # See if we can use colors.
  autoload colors zsh/terminfo
  if [[ "$terminfo[colors]" -ge 8 ]]; then
    colors
  fi

  for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE BLACK; do
    eval PR_$color='%{$fg[${(L)color}]%}'
    eval PR_LIGHT_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
    (( count = $count + 1 ))
  done
  PR_NO_COLOUR="%{$terminfo[sgr0]%}"

  for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
    eval PR_BG_$color='%{$bg[${(L)color}]%}'
  done
  PR_BG_NO_COLOUR="%{$terminfo[sgr0]%}"
  ###
  # Finally, the prompt.
  PROMPT='%K{238} $PR_RED $(prompt_char) $PR_WHITE %~ ${vcs_info_msg_0_}$PR_MAGENTA %(?/%F{yellow}/%F{red}) %k%F{238}$PR_NO_COLOUR '
  RPROMPT="%{$fg_bold[cyan]%} [% INSERT ]% %{$reset_color%}"
  PS2='$PR_RED → $PR_BLUE %_ $PR_RED → $PR_NO_COLOUR '

}

setprompt

# Make rprompt also show vi mode
function zle-line-init zle-keymap-select {
  VIM_NORMAL="%{$fg_bold[yellow]%} [% NORMAL ]% %{$reset_color%}"
  VIM_INSERT="%{$fg_bold[cyan]%} [% INSERT ]% %{$reset_color%}"
  RPROMPT="${${KEYMAP/vicmd/$VIM_NORMAL}/(main|viins)/$VIM_INSERT}"
  zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
